
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьТабличныйДокумент();	
	
	//КэшированныеЗначения = Новый Структура;
	//КэшированныеЗначения.Вставить("ВыделенныеОбласти", Новый Соответствие);
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПредыдущиеРеквизитыОбласти = НовоеОписаниеОбластиЯчеекТабличногоДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы


&НаКлиенте
Процедура СезоннаяСеткаПриАктивизацииОбласти(Элемент)
	
	//ТекущаяОбласть = Элемент.ТекущаяОбласть;
	//
	////NOTE: активизируем ПредыдущиеРеквизитыОбласти
	//Если ПредыдущиеРеквизитыОбласти <> Неопределено 
	//		И ПредыдущиеРеквизитыОбласти.Имя <> ТекущаяОбласть.Имя Тогда
	//		
	//	ЗаполнитьЗначенияСвойств(ПредыдущиеРеквизитыОбласти,ТекущаяОбласть);
	//	
	//КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СезоннаяСеткаПриИзменении(Элемент)
	
	ОчиститьСообщения();	
	ТекущаяОбласть = Элемент.ТекущаяОбласть;
	Префикс = "new";	
	ТекущаяОбласть.Имя = СтрШаблон("%1_%2",Префикс,Строка(Новый УникальныйИдентификатор));
	
КонецПроцедуры

&НаКлиенте
Процедура СеткаПриИзменении(Элемент)
	
	СеткаПриИзмененииНаСервере();

КонецПроцедуры

&НаСервере
Процедура СеткаПриИзмененииНаСервере()
	
	ИнициализироватьТабличныйДокумент();
	
	Если ЗначениеЗаполнено(Объект.Сетка) Тогда
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.Ссылка,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.ДеньНачала,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.ДеньОкончания,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.ВремяНачала,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.ВремяОкончания,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.НомерПервойКолонки,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.НомерПервойСтроки,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.НомерПоследнейСтроки,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.НомерПоследнейКолонки,
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.Текст
		|ИЗ
		|	Документ.слм_ТелевизионнаяСетка.СезонныеТаймБенды КАК слм_ТелевизионнаяСеткаСезонныеТаймБенды
		|ГДЕ
		|	слм_ТелевизионнаяСеткаСезонныеТаймБенды.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Сетка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	ЛинияОбведения = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1);
	Префикс = "new";
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьСлота = СезоннаяСетка.Область(Выборка.НомерПервойСтроки,
							Выборка.НомерПервойКолонки,
							Выборка.НомерПоследнейСтроки,
							Выборка.НомерПоследнейКолонки);
		
		ОбластьСлота.Текст = Выборка.Текст;
		ОбластьСлота.ЦветФона = WebЦвета.НейтральноЗеленый;
		ОбластьСлота.Имя = СтрШаблон("%1_%2",Префикс,Строка(Новый УникальныйИдентификатор));
		
		ОбластьСлота.Объединить();		
		ОбластьСлота.Обвести(ЛинияОбведения,ЛинияОбведения,ЛинияОбведения,ЛинияОбведения);
		
	КонецЦикла;
	
КонецЕсли; 
	
	
	
КонецПроцедуры



#КонецОбласти

#Область Формулы


&НаСервере
Функция ПолучитьДиапазонСлота(Верх, Низ, Лево, Право, Текст = "")
	
	ДиапазонСлота = Новый Структура("
		 |ДеньНачала,
		 |ДеньОкончания,
		 |ВремяНачала,
		 |ВремяОкончания,
		 |НомерПервойКолонки,
		 |НомерПервойСтроки,
		 |НомерПоследнейСтроки,
		 |НомерПоследнейКолонки,
		 |Текст");

	ДиапазонСлота.ВремяНачала		= Дата('19000101000000' + ((Верх-1) * 15 * 60)); // -1 отступ шапки
	ДиапазонСлота.ВремяОкончания 	= Дата('19000101000000' + (Низ * 15 * 60));
	ДиапазонСлота.ДеньНачала		= ПолучитьДеньНеделиПоНомеру(Лево);
	ДиапазонСлота.ДеньОкончания		= ПолучитьДеньНеделиПоНомеру(Право);
	ДиапазонСлота.НомерПервойСтроки 	= Верх;
	ДиапазонСлота.НомерПервойКолонки 	= Лево;
	ДиапазонСлота.НомерПоследнейСтроки 	= Низ;
	ДиапазонСлота.НомерПоследнейКолонки	= Право;
	ДиапазонСлота.Текст					= Текст;
	
	Возврат ДиапазонСлота;
	
КонецФункции //ПолучитьДиапазонСлота

&НаСервереБезКонтекста
Функция ПолучитьДеньНеделиПоНомеру(Знач Номер = 2)
	
	ДниНедели = Новый Соответствие;
		
	ДниНедели.Вставить(2,Перечисления.слм_ДниНедели.Понедельник);
	ДниНедели.Вставить(3,Перечисления.слм_ДниНедели.Вторник);
	ДниНедели.Вставить(4,Перечисления.слм_ДниНедели.Среда);
	ДниНедели.Вставить(5,Перечисления.слм_ДниНедели.Четверг);
	ДниНедели.Вставить(6,Перечисления.слм_ДниНедели.Пятница);
	ДниНедели.Вставить(7,Перечисления.слм_ДниНедели.Суббота);
	ДниНедели.Вставить(8,Перечисления.слм_ДниНедели.Воскресенье);
	
	Возврат ДниНедели.Получить(Номер);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьНомерДняНеделиПоЗначению(Знач ДеньНедели)
	
	ДниНедели = Новый Соответствие;
		
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Понедельник,2);
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Вторник,3);
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Среда,4);
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Четверг,5);
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Пятница,6);
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Суббота,7);
	ДниНедели.Вставить(Перечисления.слм_ДниНедели.Воскресенье,8);
	
	Возврат ДниНедели.Получить(ДеньНедели);
	
КонецФункции


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Инициализировать(Команда)
	
	ИнициализироватьТабличныйДокумент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПосчитатьВсеСлоты(Команда)
	
	ОчиститьСообщения();	
	Колонки = СезоннаяСетка.ШиринаТаблицы;
	Строки = СезоннаяСетка.ВысотаТаблицы;
	
	Для НомерКолонки = 2 По Колонки Цикл
		
		Для НомерСтроки = 2 По Строки Цикл
			
			Область = СезоннаяСетка.Область(СтрШаблон("R%1C%2", ФорматЧислаСтрокой(НомерСтроки), ФорматЧислаСтрокой(НомерКолонки)));
			ТекстЯчейки = ТекстЯчейки(СезоннаяСетка,НомерСтроки,НомерКолонки);
			
			Если ЗначениеЗаполнено(ТекстЯчейки) Тогда
				
				Область = СезоннаяСетка.Область(СтрШаблон("R%1C%2", ФорматЧислаСтрокой(НомерСтроки), ФорматЧислаСтрокой(НомерКолонки)));
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = СтрШаблон("Текст:%1, Адрес:R%2C%3", ТекстЯчейки, НомерСтроки,НомерКолонки);
				Сообщение.Сообщить();
				
			КонецЕсли; 
			
		КонецЦикла;
		
	КонецЦикла; 	
	
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьСуперСлоты(Команда)
	
	СохранитьСуперСлотыНаСервере();	  	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ТекстЯчейки(ТабличныйДокумент, НомерСтроки, НомерКолонки)

	Возврат ТабличныйДокумент.Область(СтрШаблон("R%1C%2", ФорматЧислаСтрокой(НомерСтроки), ФорматЧислаСтрокой(НомерКолонки))).Текст;

КонецФункции

&НаКлиенте
Функция ФорматЧислаСтрокой(Число)

	Возврат Формат(Число, "ЧН=0; ЧГ=0");

КонецФункции

&НаСервере
Процедура ИнициализироватьТабличныйДокумент()
	
	СезоннаяСетка.Очистить();
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	
	ОбластьМакета = Макет.ПолучитьОбласть("ВременнаяШкала");
	СезоннаяСетка.Присоединить(ОбластьМакета);
	
	Для каждого ДеньНедели Из Перечисления.слм_ДниНедели Цикл
		
		ОбластьМакета = Макет.ПолучитьОбласть(ДеньНедели);
		СезоннаяСетка.Присоединить(ОбластьМакета);
		
	КонецЦикла; 

	СезоннаяСетка.ФиксацияСверху = 1;
	СезоннаяСетка.ФиксацияСлева = 1;
	
	Элементы.СезоннаяСетка.ТекущаяОбласть = СезоннаяСетка.Область("R2C2");
	
КонецПроцедуры

&НаКлиенте
Функция НовоеОписаниеОбластиЯчеекТабличногоДокумента()
	
	ОписаниеОбластиЯчеекТабличногоДокумента = Новый Структура;
	ОписаниеОбластиЯчеекТабличногоДокумента.Вставить("Верх", 	0);
	ОписаниеОбластиЯчеекТабличногоДокумента.Вставить("Низ", 	0);
	ОписаниеОбластиЯчеекТабличногоДокумента.Вставить("Лево", 	0);
	ОписаниеОбластиЯчеекТабличногоДокумента.Вставить("Право", 	0);
	ОписаниеОбластиЯчеекТабличногоДокумента.Вставить("Имя", 	"");
	ОписаниеОбластиЯчеекТабличногоДокумента.Вставить("Текст", 	"");
	
	Возврат ОписаниеОбластиЯчеекТабличногоДокумента;
	
КонецФункции //НовоеОписаниеОбластиЯчеек

&НаСервере
Процедура СохранитьСуперСлотыНаСервере()
	
	//NOTE: здесь перебрать все добавленные области
	Префикс = "new";	
	
	ДокументСлоты = Объект.Сетка.ПолучитьОбъект();
	СезонныеТаймБенды = ДокументСлоты.СезонныеТаймБенды;
	СезонныеТаймБенды.Очистить();
	
	Для каждого _Область Из СезоннаяСетка.Области Цикл
		
		Если СтрНайти(_Область.Имя,Префикс,НаправлениеПоиска.СНачала) > 0
			И ЗначениеЗаполнено(_Область.Текст) Тогда
			
			ДиапазонСлота = ПолучитьДиапазонСлота(_Область.Верх,_Область.Низ,_Область.Лево,_Область.Право, _Область.Текст);			
			ЗаполнитьЗначенияСвойств(СезонныеТаймБенды.Добавить(),ДиапазонСлота);		
			
		КонецЕсли;
		
	КонецЦикла;  	
	
	ДокументСлоты.Записать(РежимЗаписиДокумента.Проведение);
	
КонецПроцедуры



#КонецОбласти